apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.polygraphene.alvr"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 28
        versionName "2.4.0-alpha5"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17 -fexceptions -frtti"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            debuggable = true
            jniDebuggable = true
            renderscriptOptimLevel = 3
            ndk {
                abiFilters "arm64-v8a", "armeabi-v7a"
            }
        }
        debug {
            ndk {
                abiFilters "arm64-v8a", "armeabi-v7a"
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ["src/main/jniLibs"]
        }
    }
    // Remove osig file from release apk
    applicationVariants.all { variant ->
        if (variant.buildType.name == 'release') {
            variant.mergeAssetsProvider.get().doLast {
                delete(fileTree(dir: variant.mergeAssets.outputDir, includes: ['oculussig_*']))
            }
        }
    }
    flavorDimensions "distribute", "ar", "sdk"
    productFlavors {
        Github {
            applicationId 'com.polygraphene.alvr'
            dimension 'sdk'
            manifestPlaceholders = [filter: "android.intent.category.LAUNCHER"]
        }
        OculusStore {
            applicationId 'com.polygraphene.alvr.oculus'
            dimension "distribute"
            manifestPlaceholders = [filter: "android.intent.category.INFO"]
        }
        Normal {
            dimension "ar"
        }
        ARCore {
            dimension "ar"
            applicationIdSuffix '.ar'
            minSdkVersion 24
        }
        Ovr {
            // Oculus Mobile SDK
            dimension "sdk"
            externalNativeBuild {
                cmake {
                    arguments "-DVRSDK=ovr"
                }
            }
        }
        Daydream {
            // Google VR SDK
            dimension "sdk"
            applicationIdSuffix ".daydream"
            externalNativeBuild {
                cmake {
                    arguments "-DVRSDK=gvr"
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    buildToolsVersion = '29.0.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.so'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.koushikdutta.async:androidasync:2.2.1'
    implementation 'com.google.code.gson:gson:2.8.5'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    ARCoreImplementation('com.google.ar:core:1.2.1')
    DaydreamImplementation('com.google.vr:sdk-base:1.190.0')
}

build.dependsOn(':extractNdk')
